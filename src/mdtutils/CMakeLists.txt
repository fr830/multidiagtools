# Project file for Multi Diag Tools
# * Part: mdtutils *
# * Directory: mdtutils *

# Project
project (multidiagtools)

# List of source files
set (SOURCES_FILES 
          mdtAlgorithms.cpp
          mdtMath.cpp
          mdtBuffer.cpp
          # Frame
          mdtFrame.cpp
          mdtFrameAscii.cpp
          mdtFrameModbusTcp.cpp
          mdtTime.cpp
          # Frame codec
          mdtFrameCodec.cpp
          mdtFrameCodecAscii.cpp
          mdtFrameCodecScpi.cpp
          mdtFrameCodecScpiU3606A.cpp
          mdtFrameCodecModbus.cpp
          mdtFrameCodecK8055.cpp
          # Codec
          mdtCodecScpi.cpp
          # mdtError
          mdtError.cpp
          mdtErrorOut.cpp
          # File
          mdtCsvFile.cpp
          # UIC number
          mdtUicNumber.cpp
          mdtUicNumberItem.cpp
          mdtUicNumberWidget.cpp
          mdtUicNumberValidator.cpp
          # mdtFileSystem
          mdtPartitionAttributes.cpp
          mdtFileCopier.cpp
          mdtFileCopierItem.cpp
          # I/O
          mdtAbstractIo.cpp
          mdtAnalogIo.cpp
          mdtDigitalIo.cpp
          # Value
          mdtValue.cpp
          # State machine
          mdtState.cpp
          mdtStateMachine.cpp
          # Main application
          mdtApplication.cpp
    )

# Platform specific files

# Unix (Posix) specific files
if(UNIX)
  set (SOURCES_FILES ${SOURCES_FILES}
          linux/mdtPartitionAttributesPosix.cpp
  )
endif(UNIX)

# Windows specific files
if(WIN32)
  set (SOURCES_FILES ${SOURCES_FILES}
          windows/mdtPartitionAttributesWindows.cpp
  )
endif(WIN32)


# Some specific build options

# On MinGW, the -ansi flag must be removed for file mdtFileCopier.cpp,
#  because this "diseable" the availablity of _fileno() function.
# Don't found other solution than remove the -ansi flag for the directory,
#  and re-add -ansi flg for all files exept mdtFileCopier.cpp ..
# Note: if(MINGW) not work, so we use if(WIN32) ..
if(WIN32)
  remove_definitions("-ansi")
  foreach(SRCFILE ${SOURCES_FILES})
    if(NOT (${SRCFILE} STREQUAL "mdtFileCopier.cpp"))
      set_source_files_properties(${SRCFILE} PROPERTIES COMPILE_FLAGS "-ansi")
    endif(NOT (${SRCFILE} STREQUAL "mdtFileCopier.cpp"))
  endforeach(SRCFILE)
endif(WIN32)

# Output library
add_library (mdtutils${MDTLIB_VERSION_SUFFIX} SHARED ${SOURCES_FILES})
target_link_libraries(mdtutils${MDTLIB_VERSION_SUFFIX} Qt5::Core)
target_link_libraries(mdtutils${MDTLIB_VERSION_SUFFIX} "qtsingleapplication${MDTLIB_VERSION_SUFFIX}")

# Translations - We generate a .pro file, that simplify the usage of lupdate
file(WRITE "${PROJECT_SOURCE_DIR}/../../translations/mdtutils.pro" "# Translations project file for lupdate/lrelease\n")
file(APPEND "${PROJECT_SOURCE_DIR}/../../translations/mdtutils.pro" "# This file is generated by CMake, all changes will be lost\n")
file(APPEND "${PROJECT_SOURCE_DIR}/../../translations/mdtutils.pro" "TRANSLATIONS = ${PROJECT_SOURCE_DIR}/../../translations/mdtutils_fr.ts\n")
file(APPEND "${PROJECT_SOURCE_DIR}/../../translations/mdtutils.pro" "TRANSLATIONS += ${PROJECT_SOURCE_DIR}/../../translations/mdtutils_de.ts\n")
foreach(SRCFILE ${SOURCES_FILES})
  file(APPEND "${PROJECT_SOURCE_DIR}/../../translations/mdtutils.pro" "SOURCES += ${PROJECT_SOURCE_DIR}/${SRCFILE}\n")
endforeach(SRCFILE)
foreach(FRMFILE ${FORM_FILES})
  file(APPEND "${PROJECT_SOURCE_DIR}/../../translations/mdtutils.pro" "FORMS += ${PROJECT_SOURCE_DIR}/${FRMFILE}\n")
endforeach(FRMFILE)

# Headers to install
file(GLOB INSTALL_HEADERS *.h)
install(FILES ${INSTALL_HEADERS} DESTINATION "${HEADERS_DEST_DIR}/mdtutils" COMPONENT dev)

# Install targets
install(TARGETS "mdtutils${MDTLIB_VERSION_SUFFIX}" DESTINATION "${LIB_DEST_DIR}" COMPONENT lib)

# Strip library and put debug symbols into separate file
if(UNIX)
  add_custom_command(TARGET "mdtutils${MDTLIB_VERSION_SUFFIX}"
                    POST_BUILD
                    COMMAND objcopy --only-keep-debug "libmdtutils${MDTLIB_VERSION_SUFFIX}.so" "libmdtutils${MDTLIB_VERSION_SUFFIX}.dbg"
                    COMMAND strip --strip-debug --strip-unneeded "libmdtutils${MDTLIB_VERSION_SUFFIX}.so"
                    COMMAND objcopy --add-gnu-debuglink="libmdtutils${MDTLIB_VERSION_SUFFIX}.dbg" "libmdtutils${MDTLIB_VERSION_SUFFIX}.so"
                    COMMAND chmod -x "libmdtutils${MDTLIB_VERSION_SUFFIX}.dbg")
  install(FILES "libmdtutils${MDTLIB_VERSION_SUFFIX}.dbg" DESTINATION "${LIB_DEST_DIR}" COMPONENT dbg)
endif(UNIX)
